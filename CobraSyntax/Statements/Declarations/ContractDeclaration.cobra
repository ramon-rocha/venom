
namespace Venom.CobraSyntax

interface IContractDeclaration
	inherits ISyntaxNode

	get keyword as Cobra.Compiler.IToken
	get indent as Cobra.Compiler.IToken?
	get expressions as IReadOnlyList<of IExpression>
	get dedent as Cobra.Compiler.IToken?

class BaseContractDeclaration
	inherits SyntaxNode
	implements IContractDeclaration
	is abstract
	"""
	The base class for "require", "ensure" and "invariant" contracts
	"""

	var _validKinds as IReadOnlyDictionary<of String, Type> is readonly, shared = {
		TokenKind.InvariantKeyord: InvariantDeclaration,
		TokenKind.RequireKeyword: RequireDeclaration,
		TokenKind.EnsureKeyword: EnsureDeclaration
	}

	var _inheritanceKeyword as Cobra.Compiler.IToken?

	get keyword from var as Cobra.Compiler.IToken
	get indent from var as Cobra.Compiler.IToken?
	get expressions from var as IReadOnlyList<of IExpression>
	get dedent from var as Cobra.Compiler.IToken?

	def parse(stream as ITokenStream) as IContractDeclaration is new, shared
		require
			stream.token.kind in TokenKind.contractKeywords _
			or stream.token.kind == TokenKind.LogicalOrOperator _
			or stream.token.kind == TokenKind.LogicalAndOperator
		test
			source = "invariant true"
			stream = TokenStream(source, 'test.cobra')
			contract = BaseContractDeclaration.parse(stream)
			assert contract inherits InvariantDeclaration
		body
			inheritanceKeyword = stream.grabOptional(TokenKind.LogicalOrOperator)
			if inheritanceKeyword == nil
				inheritanceKeyword = stream.grabOptional(TokenKind.LogicalAndOperator)
			
			keyword = stream.grabOneOf(TokenKind.contractKeywords)
			stream.skipBlankLines

			indent = stream.grabOptional(TokenKind.Indent)
			exprs = ExpressionList()
			if indent
				while not stream.token.endsBlock
					exprs.add(Expression.parse(stream))
					stream.skipBlankLines
				dedent = stream.expect(TokenKind.Dedent) to ?
			else
				exprs.add(Expression.parse(stream))

			typ = _validKinds[keyword.kind]
			return typ(inheritanceKeyword, keyword, indent, exprs, dedent)

	cue init(
		inheritanceKeyword as Cobra.Compiler.IToken?,
		keyword as Cobra.Compiler.IToken,
		indent as Cobra.Compiler.IToken?,
		expressions as IExpression*,
		dedent as Cobra.Compiler.IToken?
	)
		require
			keyword.kind in TokenKind.contractKeywords
			dedent <> nil implies indent <> nil
			indent <> nil implies dedent <> nil
		ensure
			.dedent == nil implies .indent == nil
		body
			base.init(keyword, nil)
			if inheritanceKeyword <> nil
				_inheritanceKeyword = _firstToken = inheritanceKeyword
			_keyword = keyword
			if dedent <> nil
				_dedent = _lastToken = dedent
				_indent = indent
			_expressions = ExpressionList(expressions)
			for e in _expressions, _addChild(e)

	def toCobraSource as String is override
		if _indent == nil
			e = _expressions[0]
			return "[_keyword.text] [e.toCobraSource]"

		sb = StringBuilder(_keyword.text)
		# TODO: indent
		for e in _expressions, sb.appendLine(e.toCobraSource)
		return sb.toString

class InvariantDeclaration
	inherits BaseContractDeclaration
	invariant # much meta
		.keyword.kind == TokenKind.InvariantKeyord
		_inheritanceKeyword == nil

class RequireDeclaration
	inherits BaseContractDeclaration
	invariant
		.keyword.kind == TokenKind.RequireKeyword
		_inheritanceKeyword <> nil implies _inheritanceKeyword.kind == TokenKind.LogicalOrOperator

	get orKeyword  as Cobra.Compiler.IToken?
		return _inheritanceKeyword

	def parse(stream as ITokenStream) as RequireDeclaration is new, shared
		require
			stream.token.kind == TokenKind.RequireKeyword _
			or stream.token.kind == TokenKind.LogicalOrOperator
		test
			source = "require 1 + 1 == 2"
			stream = TokenStream(source, 'test.cobra')
			contract = RequireDeclaration.parse(stream)
			assert contract.indent == contract.dedent == nil
			assert contract.expressions.count == 1
			assert contract.expressions[0] inherits EqualityComparisonExpression
			assert contract.orKeyword == nil
			assert contract.firstToken.kind == TokenKind.RequireKeyword
		test
			source = "or require 1 + 1 == 2"
			stream = TokenStream(source, 'test.cobra')
			contract = RequireDeclaration.parse(stream)
			assert contract.orKeyword <> nil
			assert contract.firstToken.kind == TokenKind.LogicalOrOperator
			assert contract.indent == contract.dedent == nil
			assert contract.expressions.count == 1
			assert contract.expressions[0] inherits EqualityComparisonExpression
		test
			source =
			"require\n" +
			"    1 + 1 == 2\n" +
			"    true or false"
			stream = TokenStream(source, 'test.cobra')
			contract = RequireDeclaration.parse(stream)
			assert contract.indent <> nil
			assert contract.dedent <> nil
			assert contract.expressions.count == 2
		body
			return BaseContractDeclaration.parse(stream) to RequireDeclaration


class EnsureDeclaration
	inherits BaseContractDeclaration
	invariant
		.keyword.kind == TokenKind.EnsureKeyword
		_inheritanceKeyword <> nil implies _inheritanceKeyword.kind == TokenKind.LogicalAndOperator

	get andKeyword as Cobra.Compiler.IToken?
		return _inheritanceKeyword

	def parse(stream as ITokenStream) as EnsureDeclaration is new, shared
		require
			stream.token.kind == TokenKind.EnsureKeyword _
			or stream.token.kind == TokenKind.LogicalAndOperator
		test
			source = "ensure true <> false"
			stream = TokenStream(source, 'test.cobra')
			contract = EnsureDeclaration.parse(stream)
			assert contract.andKeyword == nil
			assert contract.firstToken.kind == TokenKind.EnsureKeyword
		test
			source = "and ensure true <> false"
			stream = TokenStream(source, 'test.cobra')
			contract = EnsureDeclaration.parse(stream)
			assert contract.andKeyword <> nil
			assert contract.firstToken.kind == TokenKind.LogicalAndOperator
		body
			return BaseContractDeclaration.parse(stream) to EnsureDeclaration