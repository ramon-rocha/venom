
namespace Venom.CobraSyntax

class ExtensionDeclaration
	inherits MemberDeclaration
	implements INamespaceMemberDeclaration
	"""
	The syntax for a declaration that extends an existng type.
	"""

	# TODO: Add properties and constructor to set backing vars

	def parse(stream as ITokenStream) as ExtensionDeclaration is shared, new
		require
			stream.token.kind == TokenKind.ExtendKeyword
		test
			source =
			"extend String\n" +
			"    def double as String\n" +
			"        return this + this"
			stream = TokenStream(source, 'test.cobra')
			ext = ExtensionDeclaration.parse(stream)
			assert ext.identifier.name == "String"
			children = List<of ISyntaxNode>(ext.children)
			assert children.count == 2
			assert children[0] inherits QualifiedIdentifier
			assert children[1] implements IMethodDeclaration
		body
			kw = stream.expect(TokenKind.ExtendKeyword)
			id = QualifiedIdentifier.parse(stream)
			stream.skipBlankLines

			indent = stream.expect(TokenKind.Indent)

			if stream.token.startsDocString
				docString = DocString.parse(stream) to ?
				stream.skipBlankLines

			decls = List<of ITypeMemberDeclaration>()
			while not stream.token.endsBlock
				decls.add(TypeMemberDeclaration.parse(stream))
				stream.skipBlankLines

			dedent = stream.expect(TokenKind.Dedent)

			return ExtensionDeclaration(kw, id, indent, docString, dedent, decls, nil)
