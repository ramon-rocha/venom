
namespace Venom.CobraSyntax

interface IMemberDeclaration
	inherits IStatement, INamedNode, IDocumentable
	"""
	A statement that declares a member of a type or namespace
	"""
	pass

class ErrorMember
	inherits SyntaxNode
	implements IMemberDeclaration
	adds NamedBehavior, DocumentationBehavior
	"""
	Represents an unknown member declaration or other invalid member construct
	"""

	invariant
		.extraTokens.count == 1
		.extraTokens[0] is .firstToken
		.firstToken is .lastToken

	cue init(token as Cobra.Compiler.IToken)
		base.init(token, nil)
		_extraTokens = TokenList([token])
		_name, _docString = "", nil


class MemberDeclaration
	inherits Statement
	implements IMemberDeclaration
	adds NamedBehavior, DocumentationBehavior
	is abstract
	"""
	This is the base class for member declarations.  This includes namespaces,
	type definitions, fields, properties, and methods. However, this does not
	include local variables, method parameters, or use directives.
	"""

	get identifier from var as Identifier
	get indentToken from var as Cobra.Compiler.IToken?
	get dedentToken from var as Cobra.Compiler.IToken?

	invariant
		_indentToken implies _dedentToken
		_dedentToken implies _indentToken
	
	cue init(
		keywordToken as Cobra.Compiler.IToken?,
		identifier as Identifier,
		indentToken as Cobra.Compiler.IToken?,
		docString as DocString?,
		dedentToken as Cobra.Compiler.IToken?,
		children as ISyntaxNode*?,
		extraTokens as IReadOnlyTokenList?
	)
		require
			keywordToken <> nil implies keywordToken.isKeyword

			# member declarations (other than namespaces) that have a doc string must have both an indent and dedent token
			not (this inherits NamespaceDeclaration) implies _
				(docString implies (indentToken and dedentToken))
		ensure
			_firstToken is keywordToken or _firstToken is identifier.firstToken
			dedentToken implies _lastToken == dedentToken
		body
			base.init(keywordToken ? identifier.firstToken, identifier.lastToken, extraTokens)
			_identifier = identifier
			_name = identifier.name
			_addChild(identifier)
			if dedentToken <> nil
				_indentToken, _dedentToken = indentToken, dedentToken
				_lastToken = dedentToken
			if docString <> nil
				_addChild(docString)
				_docString = docString
			if children <> nil
				_addChildren(children)


interface ITypeMemberDeclaration inherits IMemberDeclaration
	"""A member declaration that appears inside a type declaration"""
	pass

class ErrorTypeMember
	inherits ErrorMember
	implements ITypeMemberDeclaration
	"""
	Represents an unknown member declaration appearing inside a type
	or other invalid type member construct
	"""


class TypeMemberDeclaration
	inherits MemberDeclaration
	implements ITypeMemberDeclaration
	is abstract
	"""
	The base class for a member declaration that appears inside a type declaration
	"""

	def parse(stream as ITokenStream) as ITypeMemberDeclaration? is new, shared
		
		stream.skipBlankLines
		kind = stream.token.kind

		if kind in TokenKind.typeDeclarationKeywords
			return TypeDeclaration.parse(stream)
		
		else if kind in TokenKind.modifierKeywords
			return ModifierBlock.parse(stream)
		
		branch kind
			on TokenKind.CueKeyword, return CueDeclaration.parse(stream)
			on TokenKind.MethodKeyword, return MethodDeclaration.parse(stream)

			on TokenKind.GetterKeyword _
			or TokenKind.SetterKeyword _
			or TokenKind.PropertyKeyword
				return PropertyDeclaration.parse(stream) to ITypeMemberDeclaration?

			on TokenKind.FieldKeyword, return FieldDeclaration.parse(stream)
			on TokenKind.ConstKeyword, return ConstantDeclaration.parse(stream)
			on TokenKind.InvariantKeyord, return InvariantDeclaration.parse(stream)
			on TokenKind.EventKeyword, return EventDeclaration.parse(stream)
			on TokenKind.TestKeyword, return TestDeclaration.parse(stream)
		
		return ErrorTypeMember(stream.grab)

class EnumMemberDeclaration
	inherits TypeMemberDeclaration
	"""
	An enumeration member declaration.
	"""

	get assignToken from var as Cobra.Compiler.IToken?

	get explicitValue from var as IExpression?

	def parse(stream as ITokenStream) as EnumMemberDeclaration is new, shared
		id = Identifier.parse(stream)
		if stream.token.kind == TokenKind.AssignmentOperator
			assignToken = stream.grab to ?
			val = Expression.parse(stream) to ?
		return EnumMemberDeclaration(id, assignToken, val)

	cue init(id as Identifier, assignToken as Cobra.Compiler.IToken?, val as IExpression?)
		base.init(nil, id, nil, nil, nil, nil, nil)
		_assignToken = assignToken
		if val <> nil
			_explicitValue = val
			_addChild(val)