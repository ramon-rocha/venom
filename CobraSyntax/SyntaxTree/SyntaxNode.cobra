use ICSharpCode

namespace Venom.CobraSyntax

interface ISyntaxNode
	"""
	Each node can represent a statement, expression, or other Cobra language syntax construct.
	"""
	
	get fileName as String
		"""
		The name of the file where this node was created from.
		"""
	
	get firstToken as Cobra.Compiler.IToken
		"""
		The first token that is part of the node.
		"""
	
	get lastToken as Cobra.Compiler.IToken
		"""
		The last token that is part of the node.  This may be the same token as .firstToken
		"""
	
	get beginOffset as int
		"""
		The character offset from the beginning of the source file where the node begins.
		"""
	
	get endOffset as int
		"""
		The character offset from the beginning of the source file where the node ends.
		"""

	get span as int
		"""
		The number of characters between the starting and ending points of the node in the source file.
		"""

	get beginLocation as NRefactory.TextLocation
		"""The beginning line and column number of this node."""

	get endLocation as NRefactory.TextLocation
		"""The ending line and column number of this node."""
	
	pro parent as ISyntaxNode?
		"""The immediate parent of this node in the AST."""
	
	get root as ISyntaxNode
		"""The top-most node in the AST."""
	
	get children as ISyntaxNode*
		"""The stream of child nodes under this node."""
	
	get childCount as int
		"""The number of child nodes under this node."""
	
	get extraTokens as IReadOnlyTokenList
		"""
		A list of tokens that were not expected to be contained in this node but are.
		"""

	get errors as IReadOnlyList<of NRefactory.TypeSystem.Error>
		"""
		Any parsing or syntax errors present in this node.
		"""


class NodeList inherits List<of ISyntaxNode>
	pass

class SyntaxNode
	implements ISyntaxNode
	is abstract
	"""
	This is the base class from which all other nodes in the syntax tree inherit.
	"""


	invariant
		
		_firstToken.charNum <= _lastToken.charNum

		/# # doesn't work due to compiler bug
		all for c in .children get _firstToken.charNum <= c.firstToken
		all for c in .children get c.lastToken <= _lastToken.charNum #/

		#.errors.count >= .extraTokens.count # but why doesn't this work?

	shared
		def parse(stream as ITokenStream) as ISyntaxNode
			throw NotImplementedException()
	
	cue init
		.init(nil)
	
	cue init(extraTokens as TokenList?)
		.init(Cobra.Compiler.Token.empty, extraTokens)
	
	cue init(child as ISyntaxNode, extraTokens as IReadOnlyTokenList?)
		.init(child.firstToken, child.lastToken, extraTokens)
		_addChild(child)
	
	cue init(token as Cobra.Compiler.IToken, extraTokens as IReadOnlyTokenList?)
		.init(token, token, extraTokens)
	
	cue init(children as NodeList, extraTokens as IReadOnlyTokenList?)
		.init(children.first.firstToken, children.last.lastToken, children, extraTokens)
	
	cue init(
		firstToken as Cobra.Compiler.IToken,
		lastToken as Cobra.Compiler.IToken,
		extraTokens as IReadOnlyTokenList?
	)
		require
			not lastToken.isEmpty implies lastToken.charNum >= firstToken.charNum
		body
			.init(firstToken, lastToken, NodeList(), extraTokens)
	
	cue init(
		firstToken as Cobra.Compiler.IToken,
		lastToken as Cobra.Compiler.IToken,
		children as NodeList,
		extraTokens as IReadOnlyTokenList?
	)
		require
			not lastToken.isEmpty implies lastToken.charNum >= firstToken.colNum
			not lastToken.isEmpty and children.count <> 0 implies _
				lastToken.charNum + lastToken.length >= children.last.endOffset
		ensure
			_extraTokens is not extraTokens
			extraTokens <> nil implies _extraTokens == extraTokens
		body
			base.init
			_firstToken = firstToken
			_lastToken = lastToken
			_children = NodeList(children.count)
			_addChildren(children)
			if extraTokens <> nil
				_extraTokens = TokenList(extraTokens)
			else
				_extraTokens = TokenList()
	
	var _fileName as String?
	get fileName as String
		"""
		The name of the file where this node was created from.
		"""
		_fileName ?= _firstToken.fileName
		return _fileName to !
	
	get firstToken from var as Cobra.Compiler.IToken
		"""
		The first token that is part of the node.
		"""
	
	get lastToken from var as Cobra.Compiler.IToken
		"""
		The last token that is part of the node.  This may be the same token as .firstToken
		"""
	
	get beginOffset as int
		"""
		The character offset from the beginning of the source file to where the node begins.
		"""
		return _firstToken.charNum
	
	get endOffset as int
		"""
		The character offset from the beginning of the source file to where the node ends.
		"""
		ensure
			result >= .beginOffset
		body
			if _lastToken is Cobra.Compiler.Token.empty
				offset = _firstToken.charNum
				length = _firstToken.length
			else
				offset = _lastToken.charNum
				length = _lastToken.length
			return offset + length

	get span as int
		"""
		The number of characters between the starting and ending points of the node in the source file.
		"""
		ensure result >= 0
		return .endOffset - .beginOffset

	var _beginLocation = NRefactory.TextLocation.empty
	get beginLocation as NRefactory.TextLocation
		"""
		The beginning line and column number of this node.
		"""
		if _beginLocation.isEmpty
			line = _firstToken.lineNum
			column = _firstToken.colNum
			_beginLocation = NRefactory.TextLocation(line, column)
		return _beginLocation

	var _endLocation = NRefactory.TextLocation.empty
	get endLocation as NRefactory.TextLocation
		"""
		The ending line and column number of this node.
		"""
		if _endLocation.isEmpty
			if _lastToken is Cobra.Compiler.Token.empty
				line = _firstToken.lineNum
				column = _firstToken.colNum + _firstToken.text.trim.length
			else
				line = _lastToken.lineNum
				column = _lastToken.colNum + _lastToken.text.trim.length
			_endLocation = NRefactory.TextLocation(line, column)
		return _endLocation

	var _parent as ISyntaxNode?	
	pro parent as ISyntaxNode?
		"""
		The immediate parent of this node in the AST.
		"""
		get
			return _parent
		set
			require _parent == nil
			_parent = value
	
	get root as ISyntaxNode
		"""
		The top-most node in the AST.
		"""
		if .parent == nil
			return this
		else
			return .parent.root
	
	var _children as NodeList is readonly
	get children as ISyntaxNode*
		"""
		The stream of child nodes under this node.
		"""
		return _children
	
	get childCount as int
		"""
		The number of child nodes under this node.
		"""
		return _children.count
	
	def _addChild(child as ISyntaxNode)
		"""
		Adds the given node as a child to this node.  The child node should not already have a parent node.
		"""
		require child.parent == nil
		ensure child.parent is this
		_children.add(child)
		if child.lastToken.charNum > _lastToken.charNum
			_lastToken = child.lastToken
		child.parent = this
	
	def _addChildren(children as ISyntaxNode*)
		"""
		Adds the given nodes as children to this node.  The child nodes should not already have a parent node.
		"""
		for child in children, _addChild(child)
	
	get extraTokens from var as IReadOnlyTokenList
		"""
		A list of tokens that were not expected to be contained in this node.
		"""

	var _errors is readonly = List<of NRefactory.TypeSystem.Error>()
	get errors as IReadOnlyList<of NRefactory.TypeSystem.Error>
		"""
		Any parsing or syntax errors present in this node.
		"""
		return _errors

	def _addError(token as Cobra.Compiler.IToken, message as String)
		line, col = token.lineNum, token.colNum
		length = if(token.length > 0, token.length, 1)
		_addError(line, col, length, message)

	def _addError(line as int, col as int, message as String)
		require
			line > 0
			col > 0
		body
			_addError(line, col, 1, message)

	def _addError(line as int, col as int, length as int, message as String)
		require
			line > 0
			col > 0
			length > 0
		body
			region = NRefactory.TypeSystem.DomRegion(_fileName, line, col, line, col + length)
			typ = NRefactory.TypeSystem.ErrorType.Error
			err = NRefactory.TypeSystem.Error(typ, message, region)
			_addError(err)
	
	def _addError(error as NRefactory.TypeSystem.Error)
		require error not in _errors
		ensure error in _errors
		_errors.add(error)

	def _addWarning(line as int, col as int, message as String)
		require
			line > 0
			col > 0
		body
			region = NRefactory.TypeSystem.DomRegion(_fileName, line, col, line, col + 1)
			typ = NRefactory.TypeSystem.ErrorType.Warning
			warning = NRefactory.TypeSystem.Error(typ, message, region)
			_addError(warning)