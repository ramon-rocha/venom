use System.Diagnostics

namespace Venom.TestFramework

class TimedTestRunListener implements Cobra.Core.Test.ITestRunListener
	
	var _testStopwatch = Stopwatch()
	var _totalTime = 0.0
	
	def suiteStarted(ts as Test.TestSuite)
		print "[DateTime.now.toString] - Starting test suite: [ts.name]..."

	def suiteEnded(ts as Test.TestSuite)
		print "[DateTime.now.toString] - Finished test suite: [ts.name]"
		
	def testStarted(t as Test.Test)
		require not _testStopwatch.isRunning
		print "Testing [t.name]..."
		_testStopwatch.start
		
	def testEnded(t as Test.Test)
		require _testStopwatch.isRunning
		_testStopwatch.stop
		print "\tSuccess (time = [_makeDuration])"
		_testStopwatch.reset
		
	def testFailed(t as Test.Test, exc as Exception)
		require _testStopwatch.isRunning
		_testStopwatch.stop
		print "\tFailure (time = [_makeDuration])"
		print exc.toString
		
	def _makeDuration as String
		require not _testStopwatch.isRunning
		ms = _testStopwatch.elapsedMilliseconds
		if ms == 0
			return "less than 1 ms"
		else
			return "[ms.toString("###,###,###")] ms"